#pragma kernel Sort 
#pragma kernel CalculateOffsets

/*
Reference: SebLague/Fluid-Sim
*/

struct Entry {
    uint pointIndex;
	uint cellHash;
    uint cellKey;
};

RWStructuredBuffer<Entry> Entries;
RWStructuredBuffer<uint> Offsets;
const uint numEntries;
const uint groupWidth;
const uint groupHeight;
const uint stepIndex;

[numthreads(64, 1, 1)]
void Sort (uint3 id : SV_DispatchThreadID)
{
	uint i = id.x;

	uint hIndex = i & (groupWidth - 1);
	uint indexLeft = hIndex + (groupHeight + 1) * (i / groupWidth);
	uint rightStepSize = stepIndex == 0 ? groupHeight - 2 * hIndex : (groupHeight + 1) / 2;
	uint indexRight = indexLeft + rightStepSize;

	// Exit if out of bounds (for non-power of 2 input sizes)
	if (indexRight >= numEntries) return;

	uint valueLeft = Entries[indexLeft].cellKey;
	uint valueRight = Entries[indexRight].cellKey;

	// Swap entries if value is descending
	if (valueLeft > valueRight)
	{
		Entry temp = Entries[indexLeft];
		Entries[indexLeft] = Entries[indexRight];
		Entries[indexRight] = temp;
	}
}

[numthreads(64, 1, 1)]
void CalculateOffsets(uint3 id : SV_DispatchThreadID)
{
	if (id.x >= numEntries) { return; }

	uint i = id.x;
	uint null = numEntries;

	uint key = Entries[i].cellKey;
	uint keyPrev = i == 0 ? null : Entries[i - 1].cellKey;

	if (key != keyPrev)
	{
		Offsets[key] = i;
	}
}